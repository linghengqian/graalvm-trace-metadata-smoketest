plugins {
    id 'java'
    id 'org.graalvm.buildtools.native' version '0.9.19'
    id "io.freefair.lombok" version "6.6-rc1"
}

group 'com.lingh'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    implementation 'org.apache.groovy:groovy:4.0.8'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.apache.shardingsphere:shardingsphere-infra-util:5.3.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.graalvm.truffle:truffle-api:22.3.1'
}

test {
    useJUnitPlatform()
}

task copyAllDependencies(type: Copy) {
    from configurations.compileClasspath
    into "${buildDir}/resources/main/espresso-need-libs/"
}
compileTestJava {
    dependsOn(copyAllDependencies)
}

graalvmNative {
    binaries {
        test {
            buildArgs.add('--report-unsupported-elements-at-runtime')
            buildArgs.add('--language:java')
        }
    }
    agent {
        defaultMode = "standard"
        modes {
            standard {
            }
            conditional {
                userCodeFilterPath = "user-code-filter.json"
            }
        }
        metadataCopy {
            mergeWithExisting = true
            inputTaskNames.add("test")
            outputDirectories.add("src/test/resources/META-INF/native-image/org.graalvm.espresso/polyglot")
        }
    }
}